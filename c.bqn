#!/usr/bin/env bqn

⊔↩((↕1+(>⌈´))=¨<)∘⊣ /¨⟜< ↕∘≠⍠⊢

lf‿tab←•UCS 10‿9
charSet←∾charGroups←⟨
  "⍝"                   ⍝ Remark
  •d ⍝ +⟜(↕10)⌾•UCS'0'  ⍝ Digit
  "¯.π∞"                ⍝ Numeric
  ' '(⊢∾+)⌾•UCS•a       ⍝ Alphabetic
  "_"                   ⍝ Underscore
  chF←"+-×÷⋆√⌊⌈|¬∧∨<>≠=≤≥≡≢⊣⊢⥊∾≍↑↓↕⌽⍉/⍋⍒⊏⊑⊐⊒∊⍷⊔" ⍝ Function
  "˜˘¨⌜⁼´`"             ⍝ Modifier
  "∘○⊸⟜⌾⎉⚇⍟"            ⍝ Composition
  "𝕨𝕎𝕩𝕏𝕗𝔽𝕘𝔾"            ⍝ Parameter
  " "∾tab               ⍝ Whitespace
  lf∾"⋄,"               ⍝ Separator
  "←↩→"                 ⍝ Gets
  "(){}⟨⟩"              ⍝ Bracket
  "‿"                   ⍝ Ligature
  "'"""                 ⍝ Quote
⟩
bR‿bD‿bN‿bA‿bU‿bF‿bM‿bC‿bP‿bW‿bS‿bG‿bB‿bL‿bQ←↕∘≠⌾∾charGroups

Hex←16⊥(•d∾"ABCDEF")⊸⊐
MakeTab←{{(≠chF)↑⊑¨(chF⊐𝕨)⊔𝕩}○∾⟜(⥊¨∘⥊¨)´⍉(2(÷˜∾⊣)≢)⊸⥊𝕩}
tab1←MakeTab⟨
  "|-⌈⌊√" , (Hex"99")+(↕4)∾6
  "÷"     , <(Hex"10")∾1
  "⊣⊢"    , 2⥊<⟨⟩
⟩
tab2←MakeTab⟨
  "⊣"     , Hex"1A"
  "+-×÷⌊⌈", (Hex"A0")+↕6
⟩

Parse←{
  ⟨⟩Parse𝕩;args←charSet⊐𝕨⋄x←charSet⊐𝕩
  x(/˜)↩¬(x∊bW)∨<○(⌈`↕∘≠⊸×)´((⊑bS)∾bR)=⌜x
  x(/˜)↩¬(1⌾⊑1⌽1⌾⊑)⊸∧x∊bS
  a←x∊(2↑bG)⋄at←1⌽a⋄vars←at/x⋄x(/˜)↩¬a⋄a(/˜)↩¬at
  l←≠x⋄sep←x∊bS⋄x↩(bF⊑˜chF⊐<'⊣')¨⌾(sep⊸/)x
  o←x=0⊑bB⋄c←x=1⊑bB⋄v←a-˜x∊bD∾args∾vars⋄f←¬o∨c∨v∨sep
  na←(2×sep)+f×1+l↑0∾c∨v
  e←/c⋄d←+`o-c
  ed←e⊏d⋄b←(⍋⍋ed)⊏(⍋⊏⟜d)⊸⊏/o
  g←⊔ed∾0⋄s←e-(e⊏˜∾(⍋⊏⊣)¨´⍉2↕g)⌾((∾1↓g)⊸⊏)l¨e
  fe←f×(l-1)++`(-⊸≍s)⌾((e≍b)⊸⊏)l⥊0
  fe⌊↩(l-1)-l↑(⌈`↕∘≠⊸×)⌾⌽1∾˜sep
  ia←+`l↑0∾f-l↑/⁼∧f/fe
  sel←¬∘⊏⟜(o∨c)⊸/⍋(fe⌈↕l)-ia
  ⟨sel⊸⊏¨x‿na,args∾vars,vars⟩
}

f64←127-3
GenFn←{
  ⟨x‿na,vars,locs⟩←𝕩
  fns←{
    n←bD⊐<𝕩
    n=≠bD:(Hex"20")∾vars⊐<𝕩
    n=0:8⥊0
    l←2(⌊⋆⁼)n
    (Hex"44")∾2⊥˘⌽8‿8(⊣⥊×´⊸↑)∾⟨⥊0,(11⥊2)⊤1023+l,(2⥊˜0⌈l)⊤n⟩
  }‿{
    v←vars⊐<𝕩⋄v<≠vars:(Hex"22")∾v
    tab1⊑˜bF⊐<𝕩
  }‿{
    tab2⊑˜bF⊐<𝕩
  }
  ((≠∾∾)⟨locs≠⊸∾f64⟩)∾(Hex"0B")∾˜∾na⊑⟜fns⊸{𝕎𝕩}¨x
}

Gen←{
  LEB←{0≡𝕩:⥊0⋄128⊸+⌾(¯1⊸↓) 2⊥⌽⍉ (∨`⌾⌽∨´˘)⊸/ 10‿7(⊣⥊×´⊸↑) ⍉⌽(8⥊2)⊤𝕩}
  C←LEB∘≠⊸∾
  S←∾⟜C
  V←≠∾∾
  I←C∘•UCS
  t‿n‿b←𝕩
  ∾⟨
   0∾(•UCS"asm")∾4↑1
   1 S V (96∾⟜∾C¨)¨t
   3 S V ⥊¨↕≠b
   7 S V I⊸(⊣∾0∾⊢)¨⟜(↕∘≠)n
  10 S V C¨ b
  ⟩
}

Compile←{
  body←GenFn∘Parse 𝕩
  rcp←"x" GenFn∘Parse "1÷x"
  Gen ⟨⟨0‿1,1‿1⟩/¨¨f64 ⋄ ⥊<"fn" ⋄ ⟨body,rcp⟩⟩
}
